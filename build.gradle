apply plugin: 'java'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'dk.brics', name: 'automaton', version: '1.12-1'
    compile group: 'com.formdev', name: 'flatlaf', version: '0.28'
    compile group: 'com.formdev', name: 'svgSalamander', version: '1.1.2.1'
}

jar {
    baseName = 'dictionary'
    version = '0.3.9'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes('Main-Class': 'metis.dictionary.Dictionary')
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude '**/*.aut'
}

task checkDataFileFormat {
    doLast {
        File file = new File("${projectDir}/src/main/resources/resources/dictionary.data")
        file.eachLine { line, count ->
            if (!(line ==~ / .+ :: .+ /)) {
                throw new GradleException("Incorrect data file format, line: ${count}, content:'${line}'")
            }
        }
    }
}

check.dependsOn checkDataFileFormat
